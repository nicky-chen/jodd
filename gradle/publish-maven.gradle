// Copyright (c) 2003-present, Jodd Team (http://jodd.org)
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//
// 1. Redistributions of source code must retain the above copyright notice,
// this list of conditions and the following disclaimer.
//
// 2. Redistributions in binary form must reproduce the above copyright
// notice, this list of conditions and the following disclaimer in the
// documentation and/or other materials provided with the distribution.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
// AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
// ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
// LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
// CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
// SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
// INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
// CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
// ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
// POSSIBILITY OF SUCH DAMAGE.

signing {
	if (project.hasProperty('JODD_ADMIN')) {
		sign configurations.archives
	}
}

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}

uploadArchives {
	repositories.mavenDeployer {
		customizePom(pom, project)
		
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

		if (project.hasProperty('JODD_ADMIN')) {
//			snapshotRepository(url: 'file:///Users/igor/m2')
			snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
		}
	}
}

def customizePom(pom, gradleProject) {
	pom.whenConfigured { generatedPom ->

		// eliminate test-scoped dependencies (no need in maven central POMs)
		generatedPom.dependencies.removeAll { dep ->
			dep.scope == "test"
		}

		// modify POM and change scope to provided and/or add optional flag where needed
		generatedPom.dependencies.findAll {
			ext.artifactId = it.artifactId
			ext.fulldep = it.groupId + ':' + it.artifactId + ':' + it.version

			if (ext.artifactId in project.optionalDeps || ext.fulldep in project.optionalDeps) {
				it.optional = true
			}
		}

		// add all items necessary for maven central publication
		generatedPom.project {
			name = gradleProject.moduleName
			description = gradleProject.moduleDescription
			url = 'http://jodd.org'
			organization {
				name = 'Jodd'
				url = 'http://jodd.org'
			}
			issueManagement {
				system = 'GitHub'
				url = 'https://github.com/oblac/jodd/issues'
			}
			licenses {
				license {
					name 'The BSD 2-Clause License'
					url 'http://jodd.org/license.html'
					distribution 'repo'
				}
			}
			scm {
				url = 'https://github.com/oblac/jodd'
				connection = 'scm:git:git://github.com/oblac/jodd.git'
				developerConnection = 'scm:git:ssh://git@github.com/oblac/jodd.git'
			}
			developers {
				developer {
					id = 'igor'
					name = 'Igor SpasiÄ‡'
					email = 'igor@jodd.org'
					timezone = '+1'
				}
			}
		}
	}
}
